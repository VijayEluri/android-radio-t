buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:0.3.0'
    }
}

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'ssh'

version = '1.0'
mainClassName = "JettyLauncher"


repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

ssh {
    knownHosts = allowAnyHosts
}

remotes {
    vagrant {
        host = 'localhost'
        port = 2222
        user = 'vagrant'
        identity = new File('~/.vagrant.d/insecure_private_key')
    }

    production {
        host = '107.170.84.215'
        user = 'root'
        identity = new File('~/.ssh/id_rsa')
    }
}

task deployVagrant(dependsOn: 'distTar') {
    description 'Deploys the server to the local Vagrant environment'
    doLast { deployApp(remotes.vagrant) }
}

task deployProduction(dependsOn: 'distTar') {
    description 'Deploys the server to the production environment'
    doLast { deployApp(remotes.production) }
}


private void deployApp(def remote) {
    def tempPath = '/tmp/' + distTar.archiveName
    def distributionDir = '/opt'

    sshexec {
        session(remote) {
            put(distTar.archivePath.absolutePath, tempPath)

            executeSudo("rm -rf ${distributionDir}/${distTar.archiveName}")
            executeSudo("tar xf ${tempPath} -C ${distributionDir}")
        }
    }
}


dependencies {
    compile 'org.scala-lang:scala-library:2.10.3'
    compile 'org.scalatra:scalatra_2.10:2.3.0.M1'
    compile 'org.scalatra:scalatra-atmosphere_2.10:2.3.0.RC1'
    compile 'org.json4s:json4s-jackson_2.10:3.2.7'
    compile 'org.eclipse.jetty:jetty-webapp:9.1.3.v20140225'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.1.3.v20140225'
    compile 'ch.qos.logback:logback-classic:1.1.1'
    compile files('libs/smack-3.4.1-0cec571.jar', 'libs/smackx-3.4.1-0cec571.jar')

    testCompile 'org.scalatra:scalatra-scalatest_2.10:2.3.0.RC1'
    testCompile 'org.eclipse.jetty.websocket:websocket-client:9.1.3.v20140225'
}
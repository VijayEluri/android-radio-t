buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
    classpath 'com.bmuschko:gradle-docker-plugin:2.4'
  }
}

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'com.bmuschko.vagrant-base'
apply plugin: 'com.bmuschko.docker-java-application'


import com.bmuschko.gradle.vagrant.tasks.VagrantUp
import com.bmuschko.gradle.vagrant.tasks.VagrantSsh


version = '1.0'

//noinspection GroovyUnusedAssignment
mainClassName = 'ServerStarter'

repositories {
  mavenCentral()
}

docker {
  javaApplication {
    baseImage = 'java:7-jre'
    maintainer = 'Sergey Moshnikov <apps-dandelion@gmail.com>'
    port = 80
    group = 'sergeymo'
  }
}

dockerDistTar {
  runCommand "echo Europe/Helsinki > /etc/timezone && dpkg-reconfigure tzdata --frontend noninteractive"
}

task localUp(type: VagrantUp) {
  description = "Start local Vagrant box"
}

dockerBuildImage {
  dependsOn 'localUp'
}

task deployLocal(type: VagrantSsh, dependsOn: ['localUp', 'dockerBuildImage']) {
  description = 'Deploys the server to the local Vagrant environment'
  sshCommand = "/vagrant/docker/local/run-server.sh"
}


task checkProduction {
  description = "Check the production installation"
  doLast {
    def topic =  "http://radiot.tindandelion.com:8080/api/chat/v1/topic".toURL().text
    logger.info "Production server is responding, current topic is: [$topic]"
  }
}

dependencies {
  compile 'org.eclipse.jetty:jetty-webapp:9.2.11.v20150529'
  compile 'ch.qos.logback:logback-classic:1.1.3'
  compile 'ch.qos.logback:logback-access:1.1.3'
  compile 'org.scala-lang:scala-compiler:2.11.6'
  compile 'org.scala-lang:scala-library:2.11.6'
  compile 'org.json4s:json4s-jackson_2.11:3.2.11'
  compile 'org.scalatra:scalatra_2.11:2.3.1'
  compile 'org.scalatra:scalatra-json_2.11:2.3.1'

  compile files('libs/smack-3.4.1-0cec571.jar', 'libs/smackx-3.4.1-0cec571.jar')

  testCompile 'org.scalatra:scalatra-scalatest_2.11:2.3.1'
}
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:0.3.0'
        classpath 'org.gradle.api.plugins:gradle-vagrant-plugin:0.5'
    }
}

apply plugin: 'scala'
apply plugin: 'ssh'
apply plugin: 'vagrant'

version = '1.0'

repositories {
    mavenCentral()
}

ssh {
    knownHosts = allowAnyHosts
}

remotes {
    local {
        host = 'localhost'
        port = 2222
        user = 'vagrant'
        identity = new File('~/.vagrant.d/insecure_private_key')
    }

    production {
        host = '107.170.84.215'
        user = 'root'
        identity = new File('~/.ssh/id_rsa')
    }
}

task uberjar(type: Jar, dependsOn: 'classes') {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}

    manifest {
        attributes 'Main-Class': "JettyLauncher"
    }
}

task deployLocal(dependsOn: ['uberjar', 'vagrantUp']) {
    description 'Deploys the server to the local Vagrant environment'
    doLast { deployApp(remotes.local) }
}

task deployProduction(dependsOn: 'uberjar') {
    description 'Deploys the server to the production environment'
    doLast { deployApp(remotes.production) }
}


private void deployApp(def remote) {
    def tempPath = '/tmp/' + uberjar.archiveName
    def distributionDir = '/opt/radio-t-server'

    sshexec {
        session(remote) {
            put(uberjar.archivePath.absolutePath, tempPath)
            executeSudo("mv $tempPath $distributionDir")
            executeSudo("/etc/init.d/radio-t-server restart")
        }
    }
}


dependencies {
    compile 'org.scala-lang:scala-library:2.10.3'
    compile 'org.scalatra:scalatra_2.10:2.3.0.M1'
    compile 'org.scalatra:scalatra-atmosphere_2.10:2.3.0.RC1'
    compile 'org.json4s:json4s-jackson_2.10:3.2.7'
    compile 'org.eclipse.jetty:jetty-webapp:9.1.3.v20140225'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.1.3.v20140225'
    compile 'ch.qos.logback:logback-classic:1.1.1'
    compile 'commons-daemon:commons-daemon:1.0.15'
    compile files('libs/smack-3.4.1-0cec571.jar', 'libs/smackx-3.4.1-0cec571.jar')

    testCompile 'org.scalatra:scalatra-scalatest_2.10:2.3.0.RC1'
    testCompile 'org.eclipse.jetty.websocket:websocket-client:9.1.3.v20140225'
}